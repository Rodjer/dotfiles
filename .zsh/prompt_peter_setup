function get_nr_CPUs() {
  if [[ "$OSTYPE" =~ ^darwin ]]; then
    sysctl -n hw.ncpu
  else
    grep -c "^processor" /proc/cpuinfo
  fi
}

function virtualenv_info {
  [ $VIRTUAL_ENV ] && echo '('`basename $VIRTUAL_ENV`') '
}

function prompt_peter_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="${SOLAR_CYAN}%b%f%u%c?%f"
  else
    branch_format="${SOLAR_CYAN}%b%f%u%c"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "on ${branch_format}"

  vcs_info 'prompt'
}

function prompt_peter_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_peter_precmd

  # Solarized Dark colour scheme
  SOLAR_YELLOW=$(tput setaf 136)
  SOLAR_ORANGE=$(tput setaf 166)
  SOLAR_RED=$(tput setaf 124)
  SOLAR_MAGENTA=$(tput setaf 125)
  SOLAR_VIOLET=$(tput setaf 61)
  SOLAR_BLUE=$(tput setaf 33)
  SOLAR_CYAN=$(tput setaf 37)
  SOLAR_GREEN=$(tput setaf 64)
  SOLAR_WHITE=$(tput setaf 254)

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="%b%f%u%c"
  local action_format="%a%f"
  local unstaged_format="!%f"
  local staged_format="%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  load=`uptime | awk '{print $(NF-2)}' | sed 's/,$//g'`

  PROMPT='${reset_color}
'                                               # reset + newline
  PROMPT+='${SOLAR_ORANGE}%n$(virtualenv_info)' # user
  PROMPT+='${SOLAR_WHITE}@'                     # @
  PROMPT+='${SOLAR_YELLOW}%m'                   # host
  PROMPT+='${SOLAR_BASE3}:'                     # path char
  PROMPT+='${SOLAR_GREEN}%~'                    # pwd
  PROMPT+=' ${reset_color}${vcs_info_msg_0_}'   # git branch info

  # PROMPT+='$(git_remote_status)'                  # git remote status
  PROMPT+='
${reset_color}'                                 # newline + reset
  PROMPT+='%# '                                 # prompt char

  RPROMPT='load:$load Â· %T'
}

prompt_peter_setup "$@"
